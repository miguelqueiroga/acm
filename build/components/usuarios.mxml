<?xml version="1.0" encoding="utf-8"?>
<mx:TitleWindow creationComplete="{init()}" close="{PopUpManager.removePopUp(this)}" xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" title="Usuários do Sistema" width="620" height="388">
	<mx:Script>
		<![CDATA[
			import mx.collections.SortField;
			import mx.collections.Sort;
			import entity.Usuario;
			import mx.events.ValidationResultEvent;
			import mx.validators.Validator;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			import mx.events.CloseEvent;
			import mx.managers.PopUpManager;
			import mx.core.Application;
			import mx.controls.Alert;
			import mx.utils.ObjectUtil;
			import mx.collections.ArrayCollection;
			
			[Bindable]
			public var lista:ArrayCollection; 
			public var idSelecionado:int = 0;
            

			[Bindable]
			public var itens: Array = [{label:"Administrador", data:"1"}, {label:"Sócia", data:"2"}, 
                {label:"Contador", data:"3"}];
			
			public function init():void{
				habilitaValidadores(false);
				listaUsuarios();
			}
			
			public function listaUsuarios():void{
				usuarioService.getList();
			}
			
			public function hrdpGrid(event:ResultEvent):void{
				lista = (ArrayCollection)(event.result);
				lista.sort = new Sort();
                lista.sort.fields = [new SortField("usuLogin", false, false)];
                lista.refresh();
			}
			
			public function carregaDados():void{
				if(lista.length > 0) {
					if(dg.selectedIndex < 0)
						dg.selectedIndex = 0;
					var i : int;
					idSelecionado = dg.selectedItem.usuId;
					nome.text = dg.selectedItem.usuLogin;
					senha.text = dg.selectedItem.usuSenha;
					for ( i=0; i<itens.length; i++){
						if (dg.selectedItem.usuNivel==itens[i].label)
							nivel.selectedIndex = i;
					}
				}			
			}
			
			public function alterar():void{
				if ( dg.selectedIndex == -1)
					Alert.show("Selecione um Registro!");
				else{
					habilitaValidadores(true);
					habilitaEdic(true);
					carregaDados();
					habilitaAlter(true);
					nome.setFocus();
				}
			}
			
			public function habilitaEdic(hab:Boolean):void{
				nome.enabled = hab;
				senha.enabled = hab;
				nivel.enabled = hab;
			}
			
			public function cancelaOper():void{
				limpaCampos();
				habilitaEdic(false);
				habilitaAlter(false);
				carregaDados();
				habilitaValidadores(false);					
			}
			
			public function criaNovo():void{
				habilitaValidadores(true);
				limpaCampos();
				habilitaEdic(true);
				habilitaAlter(true);
				nome.setFocus();
			}
			
			public function limpaCampos():void{
				idSelecionado = 0;
				nome.text = "";
				senha.text = "";
				nivel.text = "";
			}
			
			public function habilitaAlter(hab:Boolean):void{
				btnNovo.enabled = !hab;
				btnAlterar.enabled = !hab;
				btnSalvar.enabled = hab;
				btnCancelar.enabled = hab;
				btnRemover.enabled = !hab;
				btnFechar.enabled = !hab;
				pdg.enabled = !hab;			
			}
			
			public function habilitaValidadores(hab:Boolean):void{
				validadorNome.enabled = hab;	
			}
			
			public function usuarioGravado(event:ResultEvent):void{
				habilitaValidadores(false);
				limpaCampos();
				habilitaEdic(false);
				habilitaAlter(false);
				Alert.show( "Usuário salvo com sucesso!" );
				listaUsuarios();
			}
			
			public function usuarioRemovido(event:ResultEvent):void{
				limpaCampos();
				listaUsuarios();
				Alert.show( "Usuário removido com sucesso!" );				
			}
			
			public function erroGravando(event:FaultEvent):void{
				Alert.show( ObjectUtil.toString(event.fault));
			}
			
			public function getParameters():Usuario{
				var usu : Usuario = new Usuario();
				usu.usuId = idSelecionado;
				usu.usuLogin = nome.text;
				usu.usuSenha = senha.text;
				usu.usuNivel = nivel.text;
				
				
				return usu;
			}
			
			public function salvarUsuario():void{
				 if (validar()){
				 	if(Application.application.isAdministrador()) {
						usuarioService.save( this.getParameters());
				 	}
				 	else {
				 		Alert.show("Você não possui permissão \npara salvar registros!");
				 	}				 		
				 } 
			}
			
			public function removerSelecionado():void{
				Alert.yesLabel = "Sim";
			  	Alert.noLabel = "Não";
			  	Alert.show("Deseja remover este Usuário?","Remover", Alert.YES | Alert.NO, this, confirmaRemocao);				 
			}
			
			public function confirmaRemocao(event:CloseEvent):void{
				if (event.detail==Alert.YES) {
					if(dg.selectedIndex >= 0){
						if(Application.application.isAdministrador()){
							usuarioService.remove( this.getParameters() );
						}
						else {
							Alert.show("Você não possui permissão \npara remover este registro!");
						}
					}
					else {
				 		Alert.show("Selecione um Registro!");
				 	}			
				}
			}
			
			public function validar():Boolean{
				var valido : Boolean;
				
				for each (var v : Validator in validadores ){
					var resultEvent : ValidationResultEvent = v.validate();
					if( resultEvent.type != ValidationResultEvent.VALID ){
						valido = false;
						break;
					} else {
						valido = true;
					}
				}
				return valido;
			}
			public function onFault(event:FaultEvent):void
			{												
				Alert.show(event.fault.rootCause.localizedMessage, "OCORREU UM ERRO");
			}
			
			public function validate(v:Validator):void{
				v.validate();								
			}
			
			public function upperCampo(campo : TextInput):void {
				var comparador: String = "àãõâêôáéíóúç";
				var texto: String = campo.text;
				var posicao: int = campo.selectionBeginIndex - 1;
				var caracter: String = texto.charAt(posicao);
				
				if (comparador.indexOf(caracter) != -1){
					var strCampo: String = campo.text;
					campo.text = strCampo.toUpperCase();
					campo.selectionBeginIndex = posicao+1;
					campo.selectionEndIndex = posicao+1;
				}				
			}
			
		]]>
	</mx:Script>
	
	<mx:Array id="validadores">
		
		<mx:StringValidator id="validadorNome" source="{nome}" minLength="2" required="true" property="text"
			requiredFieldError="Nome não pode ser vazio." tooShortError="Nome deve ter no mínimo 2 caracteres."
		/>
		
		<mx:StringValidator id="validadorSenha" source="{senha}" minLength="4" required="true" property="text"
			requiredFieldError="Senha não pode ser vazia." tooShortError="Senha deve ter no mínimo 4 caracteres."
		/> 

		<mx:StringValidator id="validadorNivel" source="{nivel}" required="true" property="text"
			requiredFieldError="Escolha um nível."
		/>
	</mx:Array>
	
	<mx:RemoteObject id="usuarioService" showBusyCursor="true"
		fault="onFault(event)" destination="usuarioService">
		<mx:method name="save" fault="onFault(event)" result="usuarioGravado(event)">
			<mx:arguments>
	            <commandArgs>{Usuario}</commandArgs>
	        </mx:arguments>
    	</mx:method>
    	<mx:method name="remove" fault="onFault(event)" result="usuarioRemovido(event)">
			<mx:arguments>
	            <commandArgs>{Usuario}</commandArgs>
	        </mx:arguments>
    	</mx:method>
    	<mx:method name="getList" fault="onFault(event)" result="hrdpGrid(event)"/>
            		
	</mx:RemoteObject>
	<mx:Label x="10" y="12" text="Nome"/>
	<mx:Label x="10" y="42" text="Senha"/>
	<mx:Label x="197" y="42" text="Nível"/>
	<mx:TextInput x="62" y="10" width="308" id="nome" enabled="false" maxChars="40" restrict="[A-Z \- \\ ÀÃÕÂÊÔÁÉÍÓÚÇ àãõâêôáéíóúç]" change="validate(validadorNome); upperCampo(this.nome)"/>
	<mx:TextInput x="62" y="40" width="111" displayAsPassword="true" id="senha" enabled="false" maxChars="14" restrict="[A-Z 0-9 \- \\]" change="validate(validadorSenha)"/>
	<mx:ComboBox dataProvider="{itens}" x="236" y="40" width="134" id="nivel" enabled="false" change="validate(validadorNivel)">
	</mx:ComboBox>
	<mx:Panel id="pdg" x="0" y="78" layout="absolute" title="Usuários Cadastrados" height="100%" width="100%">
		<mx:DataGrid id="dg" dataProvider="{lista}" x="0" y="0" width="100%"
			change="{carregaDados()}" height="100%" itemClick="{carregaDados()}">
			<mx:columns>
				<mx:DataGridColumn headerText="Código" dataField="usuId" width="60"/>
				<mx:DataGridColumn headerText="Nome" dataField="usuLogin"/>
				<mx:DataGridColumn headerText="Nível" dataField="usuNivel" width="100"/>
				<mx:DataGridColumn visible="false" width="0" dataField="usuSenha"/>
			</mx:columns>
		</mx:DataGrid>
	</mx:Panel>
	<mx:ControlBar height="56">
		<mx:Button label="Adicionar" id="btnNovo" icon="@Embed('../images/add.png')" click="{criaNovo()}"/>
		<mx:Button label="Alterar" id="btnAlterar" icon="@Embed('../images/alter.png')" click="{alterar()}"/>
		<mx:Button label="Salvar" enabled="false" id="btnSalvar" icon="@Embed('../images/ok.png')" click="{salvarUsuario()}"/>
		<mx:Button label="Cancelar" enabled="false" id="btnCancelar" icon="@Embed('../images/cancel.png')" click="{cancelaOper()}"/>
		<mx:Button label="Remover" id="btnRemover" click="{removerSelecionado()}" icon="@Embed(source='../images/menos.png')"/>
		<mx:Button label="Fechar" id="btnFechar" click="{PopUpManager.removePopUp(this)}" icon="@Embed('../images/close.png')"/>
	</mx:ControlBar>
	
</mx:TitleWindow>
